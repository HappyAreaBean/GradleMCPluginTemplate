plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "io.freefair.lombok" version "6.5.0.2"
    id 'kr.entree.spigradle' version '2.4.3'
    id "xyz.jpenilla.run-paper" version "2.1.0"
}

group = 'cc.happyareabean'
version = '1.0.0'

def mcVersion = "1.8.8"

spigot {
    commands {
        'mycmd' {
            aliases 'cmd'
        }
    }
    // if you want to exclude all [spigot.libraries]:
    // `excludeLibraries = ['*']`
}

repositories {
    mavenCentral()

    maven{ url = "https://repo.codemc.io/repository/nms/" }
    maven{ url = "https://repo.papermc.io/repository/maven-public/" }
}

dependencies {

    // Spigot
    compileOnly("org.spigotmc:spigot:${mcVersion}-R0.1-SNAPSHOT")
    compileOnly("org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT")

    // Paper
    // compileOnly("io.papermc.paper:paper-api:${mcVersion}-R0.1-SNAPSHOT")

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

def runDirectoryDir = project.hasProperty('runPaper.runDirectory') ? project.getProperty('runPaper.runDirectory') : "${project.rootProject.projectDir}/.run"
def runServerJar = project.hasProperty('runPaper.serverJar') ? project.getProperty('runPaper.serverJar') : null

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion(mcVersion)
        if (runServerJar != null)
            serverJar(runDirectory.file(runServerJar as String))
        runDirectory.set(file(runDirectoryDir))
    }
    shadowJar {
        List.of(
               "example"
        ).each {
            relocate(it, "cc.happyareabean.plugin.shaded.${it}")
        }
    }
}

java {
    sourceCompatibility = JavaLanguageVersion.of(17)
    targetCompatibility = JavaLanguageVersion.of(17)
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
}

test {
    useJUnitPlatform()
}